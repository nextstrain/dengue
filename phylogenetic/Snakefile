configfile: "defaults/config_dengue.yaml"

include: "../shared/vendored/snakemake/config.smk"
include: "rules/config.smk"

serotypes = config["serotypes"]
genes = config["genes"]

wildcard_constraints:
    serotype = "|".join(serotypes),
    gene = "|".join(genes)

rule all:
    input:
        auspice_json = expand("auspice/dengue_{serotype}_{gene}.json", serotype=serotypes, gene=genes),
        tip_frequencies_json= expand("auspice/dengue_{serotype}_{gene}_tip-frequencies.json", serotype=serotypes, gene=genes)


# remote_files.smk must be before merge_inputs.smk
include: "../shared/vendored/snakemake/remote_files.smk"
include: "rules/merge_inputs.smk"
include: "rules/prepare_sequences.smk"
include: "rules/prepare_sequences_E.smk"
include: "rules/construct_phylogeny.smk"
include: "rules/annotate_phylogeny.smk"
include: "rules/export.smk"

# Include custom rules defined in the config.
if "custom_rules" in config:
    for rule_file in config["custom_rules"]:

        include: rule_file

rule clean:
    """Removing directories: {params}"""
    params:
        "results ",
        "auspice"
    shell:
        "rm -rfv {params}"
