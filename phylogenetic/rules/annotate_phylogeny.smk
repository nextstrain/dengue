"""
This part of the workflow creates additonal annotations for the phylogenetic tree.
REQUIRED INPUTS:
    metadata            = data/metadata_all.tsv
    prepared_sequences  = results/aligned.fasta
    tree                = results/tree.nwk
OUTPUTS:
    node_data = results/*.json
    There are no required outputs for this part of the workflow as it depends
    on which annotations are created. All outputs are expected to be node data
    JSON files that can be fed into `augur export`.
    See Nextstrain's data format docs for more details on node data JSONs:
    https://docs.nextstrain.org/page/reference/data-formats.html
This part of the workflow usually includes the following steps:
    - augur traits
    - augur ancestral
    - augur translate
    - augur clades
See Augur's usage docs for these commands for more details.
Custom node data files can also be produced by build-specific scripts in addition
to the ones produced by Augur commands.
"""

rule ancestral:
    """Reconstructing ancestral sequences and mutations"""
    input:
        tree = "results/tree_{serotype}_{gene}.nwk",
        alignment = "results/aligned_{serotype}_{gene}.fasta"
    output:
        node_data = "results/nt-muts_{serotype}_{gene}.json"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --inference {params.inference}
        """

rule translate:
    """Translating amino acid sequences"""
    input:
        tree = "results/tree_{serotype}_{gene}.nwk",
        node_data = "results/nt-muts_{serotype}_{gene}.json",
        # The genbank references for the E gene are dynamically generated files located within the results folder.
        reference = lambda wildcard: "config/reference_{serotype}_{gene}.gb" if wildcard.gene in ["genome"] else "results/config/reference_{serotype}_{gene}.gb"
    output:
        node_data = "results/aa-muts_{serotype}_{gene}.json"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data} \
        """

rule traits:
    """
    Inferring ancestral traits for {params.columns!s}
      - increase uncertainty of reconstruction by {params.sampling_bias_correction} to partially account for sampling bias
    """
    input:
        tree = "results/tree_{serotype}_{gene}.nwk",
        metadata = "data/metadata_{serotype}.tsv"
    output:
        node_data = "results/traits_{serotype}_{gene}.json",
    params:
        columns = lambda wildcards: config['traits']['traits_columns'][wildcards.serotype + "_" + wildcards.gene],
        sampling_bias_correction = config['traits']['sampling_bias_correction'],
        strain_id = config.get("strain_id_field", "strain"),
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence \
            --sampling-bias-correction {params.sampling_bias_correction}
        """

rule clades:
    """Annotating serotypes / genotypes"""
    input:
        tree = "results/tree_{serotype}_genome.nwk",
        nt_muts = "results/nt-muts_{serotype}_genome.json",
        aa_muts = "results/aa-muts_{serotype}_genome.json",
        clade_defs = lambda wildcards: config['clades']['clade_definitions'][wildcards.serotype],
    output:
        clades = "results/clades_{serotype}_genome.json"
    shell:
        """
        augur clades \
            --tree {input.tree} \
            --mutations {input.nt_muts} {input.aa_muts} \
            --clades {input.clade_defs} \
            --output {output.clades}
        """
