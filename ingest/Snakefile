from snakemake.utils import min_version

min_version(
    "7.7.0"
)  # Snakemake 7.7.0 introduced `retries` directive used in fetch-sequences

# Utility functions shared across all workflows.
include: "../shared/vendored/snakemake/config.smk"

# Use default configuration values. Extend with Snakemake's --configfile/--config options.
configfile: os.path.join(workflow.basedir, "defaults/config.yaml")

serotypes = ['all', 'denv1', 'denv2', 'denv3', 'denv4']

rule all:
    input:
        expand(["results/sequences_{serotype}.fasta", "results/metadata_{serotype}.tsv"], serotype=serotypes)


include: "rules/fetch_from_ncbi.smk"
include: "rules/curate.smk"
include: "rules/split_serotypes.smk"
include: "rules/nextclade.smk"

# Include custom rules defined in the config.
if "custom_rules" in config:
    for rule_file in config["custom_rules"]:

        # Relative custom rule paths in the config are relative to the analysis
        # directory (i.e. the current working directory, or workdir, usually
        # given by --directory), but the "include" directive treats relative
        # paths as relative to the workflow (e.g. workflow.current_basedir).
        # Convert to an absolute path based on the analysis/current directory
        # to avoid this mismatch of expectations.
        include: os.path.join(os.getcwd(), rule_file)
