from snakemake.utils import min_version

min_version(
    "7.7.0"
)  # Snakemake 7.7.0 introduced `retries` directive used in fetch-sequences

if not config:

    configfile: "config/config.yaml"


send_slack_notifications = config.get("send_slack_notifications", False)

serotypes = ['all', 'denv1', 'denv2', 'denv3', 'denv4']

def _get_all_targets(wildcards):
    # Default targets are the metadata TSV and sequences FASTA files
    all_targets = expand(["results/sequences_{serotype}.fasta", "results/metadata_{serotype}.tsv"], serotype=serotypes)


    # Add additional targets based on upload config
    upload_config = config.get("upload", {})

    for target, params in upload_config.items():
        files_to_upload = params.get("files_to_upload", {})

        if not params.get("dst"):
            print(
                f"Skipping file upload for {target!r} because the destination was not defined."
            )
        else:
            all_targets.extend(
                expand(
                    [f"data/upload/{target}/{{remote_file_name}}.done"],
                    zip,
                    remote_file_name=files_to_upload.keys(),
                )
            )

    # Add additional targets for Nextstrain's internal Slack notifications
    if send_slack_notifications:
        all_targets.extend(
            [
                "data/notify/genbank-record-change.done",
                "data/notify/metadata-diff.done",
            ]
        )

    if config.get("trigger_rebuild", False):
        all_targets.append("data/trigger/rebuild.done")

    return all_targets


rule all:
    input:
        _get_all_targets,


include: "workflow/snakemake_rules/fetch_sequences.smk"
include: "workflow/snakemake_rules/transform.smk"
include: "workflow/snakemake_rules/split_serotypes.smk"
include: "workflow/snakemake_rules/nextclade.smk"


if config.get("upload", False):

    include: "workflow/snakemake_rules/upload.smk"
